buildscript {
    ext.kotlin_version = '1.0.5'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7.2'
    id 'net.researchgate.release' version '2.4.1'
}
apply plugin: 'kotlin'

group 'net.dinomite.web'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

dependencies {
    compile "com.google.guava:guava:20.0"
    compile "org.apache.httpcomponents:httpclient:4.5.2"
    compile "org.jsoup:jsoup:1.8.3"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.slf4j:slf4j-simple:1.7.18"

    testCompile "junit:junit:4.12"
    testCompile "com.github.tomakehurst:wiremock:1.58"
}

task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        bintray(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version

            artifact sourceJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

release {
    tagTemplate = 'v$version'
    afterReleaseBuild.dependsOn bintrayUpload
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['bintray']

    pkg {
        repo = 'maven'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/dinomite/unfurling'
        name = 'net.dinomite.web.unfurling'

        version {
            name = project.version
            released = new Date()
            vcsTag = 'v' + project.version
        }
    }
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}
